import java_cup.runtime.*;
import java.util.*;

parser code {:
	public static void main(String args[]) throws Exception {
		new parser(new Yylex(System.in)).parse();
	}
	
	public void syntax_error(Symbol s) {
    	System.out.println("compiler has detected a syntax error at line " + getLine() + " column " + getColumn());
		System.out.println(getText());
	}

	public int getLine() {
		return ((Yylex) getScanner()).getLine();
	}

	public int getColumn() {
		return ((Yylex) getScanner()).getColumn();
	}
	
	public String getText() {
		return ((Yylex) getScanner()).getText();
	}
:}

action code {:
	Writer writer = new Writer("CMaisonUser.java");
	Hashtable< String, String > table = new Hashtable< String, String >();
	Stack<String> stack = new Stack<String>();
	int programmerCount = 0;
	List scenarii = new ArrayList(); 
:}

terminal PROGRAMME_DOMUS_OPEN_TAG, PROGRAMME_DOMUS_CLOSE_TAG,
 DECLARATION_APPAREILS_OPEN_TAG, DECLARATION_APPAREILS_CLOSE_TAG,
 DECLARATION_INTERFACE_OPEN_TAG, DECLARATION_INTERFACE_CLOSE_TAG,
 DECLARATION_SCENARII_OPEN_TAG, DECLARATION_SCENARII_CLOSE_TAG,
 DECLARATION_COMMANDES_OPEN_TAG, DECLARATION_COMMANDES_CLOSE_TAG,
 
 DEFINIR, EXECUTOR_SCENARIO, ASSOCIER, PROGRAMMER, MESSAGE, POURTOUT,
 FAIRE, FAIT, SI, ALORS, SINON, FSI, OUVRIR, FERMER, ETEINDRE, ALLUMER,
 TAMISER, ETAT, ALLUMER_PARTIEL, ALLUMER_ECO, OUVRIR_PARTIEL, ALLUME, ETEINT,
 DEMI, ECO, OUVERT, FERME, TABLETTE, TV, HIFI, CAFETIERE, VIDEO_PROJ,
 LAVE_VAISSELLE, LAVE_LINGE, SECHE_LINGE, ORDINATEUR, PORTAIL,

 DOT, COMMA, COLON, ASSIGN, SEMI_COLON, CURLY_BRACKET_OPEN, CURLY_BRACKET_CLOSE, EQUALS, PARANTHESES_OPEN, PARANTHESES_CLOSE;

terminal String SCENARIO_OPEN_TAG,SCENARIO_CLOSE_TAG, IDENTIFIER, LINE_COMMENT, BLOCK_COMMENT, STRING_LITERAL, DATE;
// appareils
terminal String ECLAIRAGE, VOLET, CHAUFFAGE, ALARME, FENETRE, AUTRE_APPAREIL;
// interface
terminal String INTERRUPTEUR, MOBILE, TELEPHONE, TELECOMMANDE;

non terminal expr, list, item;
non terminal appareils_subitem, interfaces_subitem, autre_appareils_subitem, definir_subitem;
non terminal pourtout_subitem, pourtout_action, si_condition, si_subitem, sinon_subitem, message_subitem, programmer_subset, associer_subitem;
non terminal appareils, autre_appareils, interfaces, actions, states;

expr ::= list {:
	System.out.println("\n\nAnalyse terminee!");
:} | {: System.out.println("Entree vide!"); :};

list ::= item list | item;

// COMMENTS
item ::= LINE_COMMENT:s {: writer.appendln(s); :};
item ::= BLOCK_COMMENT:s {: writer.appendln(s); :};

// TAGS
item ::= PROGRAMME_DOMUS_OPEN_TAG:s {: writer.createFile(); :};
item ::= PROGRAMME_DOMUS_CLOSE_TAG:s {: :};

item ::= DECLARATION_APPAREILS_OPEN_TAG:s {: :};
item ::= DECLARATION_APPAREILS_CLOSE_TAG:s {: :};

item ::= DECLARATION_INTERFACE_OPEN_TAG:s {: :};
item ::= DECLARATION_INTERFACE_CLOSE_TAG:s {: :};

item ::= DECLARATION_SCENARII_OPEN_TAG:s {: :};
item ::= DECLARATION_SCENARII_CLOSE_TAG:s {: :};

item ::= SCENARIO_OPEN_TAG:s {:
	String scenario = Utils.retrieveName(s);
	scenarii.add(scenario);
	table.put("SCENARIO", scenario);
	writer.appendln("String " + scenario + "_contenu = \"\\n");
:};
item ::= SCENARIO_CLOSE_TAG:s {:
	String scenario = table.get("SCENARIO");
	writer.appendln("\";");
	writer.appendln("CScenario " + scenario + " = new CScenario(\"" + scenario + "\", " + scenario + "_contenu);");
	writer.appendln("ma_liste_scenarios.add(" + scenario + ");");

	table.remove("SCENARIO");
:};

item ::= DECLARATION_COMMANDES_OPEN_TAG:s {: :};
item ::= DECLARATION_COMMANDES_CLOSE_TAG:s {: :};

appareils ::= 
	  ECLAIRAGE:a {: RESULT = a; :} 
	| VOLET:a {: RESULT = a; :}
	| CHAUFFAGE:a {: RESULT = a; :}
	| ALARME:a {: RESULT = a; :}
	| FENETRE:a {: RESULT = a; :};

autre_appareils ::= 
	  TABLETTE:ap {: RESULT = "tablette"; :}
	| TV:ap {: RESULT = "tv"; :}
	| HIFI:ap {: RESULT = "hifi"; :}
	| CAFETIERE:ap {: RESULT = "cafe"; :}
	| VIDEO_PROJ:ap {: RESULT = "video_proj"; :}
	| LAVE_VAISSELLE:ap {: RESULT = "lave_vaisselle"; :}
	| LAVE_LINGE:ap {: RESULT = "lave_linge"; :}
	| SECHE_LINGE:ap {: RESULT = "seche_linge"; :}
	| ORDINATEUR:ap {: RESULT = "ordinateur"; :}
	| PORTAIL:ap {: RESULT = "portail"; :};

interfaces ::= 
	  INTERRUPTEUR:i {: RESULT = i; :}
	| MOBILE:i {: RESULT = i; :}
	| TELEPHONE:i {: RESULT = i; :}
	| TELECOMMANDE:i {: RESULT = i; :};

actions ::=
      OUVRIR:a {: RESULT = a; :}
	| FERMER:a {: RESULT = a; :} 
	| ETEINDRE:a {: RESULT = a; :} 
	| ALLUMER :a {: RESULT = a; :}
	| TAMISER:a {: RESULT = a; :}
	| ALLUMER_PARTIEL:a {: RESULT = a; :}
	| ALLUMER_ECO:a {: RESULT = a; :}
	| OUVRIR_PARTIEL:a {: RESULT = a; :};

states ::=
	  ALLUME :s {: RESULT = s; :}
	| ETEINT:s {: RESULT = s; :}
	| DEMI:s {: RESULT = s; :}
	| ECO:s {: RESULT = s; :}
	| OUVERT:s {: RESULT = s; :}
	| FERME:s {: RESULT = s; :};

message_subitem ::=
	  STRING_LITERAL:s {: RESULT = ((String) s).replaceAll("\"", ""); :}
	| STRING_LITERAL:s COMMA IDENTIFIER:id message_subitem {: RESULT = s; :}
	| STRING_LITERAL:s COMMA IDENTIFIER:id DOT ETAT message_subitem {: RESULT = s; :};


// APPAREIL
item ::= appareils:a appareils_subitem {:
	// TODO Update class name for VOLET to VoletFENETRE
	String appareils = (String) a;
	String className = "C" + appareils.substring(0, 1).toUpperCase() + appareils.substring(1);
	while(!stack.isEmpty()) {
		String identifier = stack.pop();
		writer.appendln(className + " " + identifier + " = new " + className + "(\"" + identifier + "\", TypeAppareil." + appareils.toUpperCase() + ");");
		writer.appendln("ma_liste_appareils.add(" + identifier + ");");
	}
:};
appareils_subitem ::=
	  IDENTIFIER:id DOT {: stack.push(id); :}
	| IDENTIFIER:id COMMA appareils_subitem {: stack.push(id); :};


// AUTRE APPAREIL
item ::= AUTRE_APPAREIL PARANTHESES_OPEN autre_appareils:ap PARANTHESES_CLOSE autre_appareils_subitem {:
	String appareils = (String) ap;
	while(!stack.isEmpty()) {
		String identifier = stack.pop();
		writer.appendln("CAutreAppareil " + identifier + " = new CAutreAppareil(\"" + identifier + "\", TypeAppareil.AUTRE_APPAREIL_" + appareils.toUpperCase() + ");");
		writer.appendln("ma_liste_appareils.add(" + identifier + ");");
	}
:};
autre_appareils_subitem ::=
	  IDENTIFIER:id DOT {: stack.push(id); :}
	| IDENTIFIER:id COMMA autre_appareils_subitem {: stack.push(id); :};


// DEFINIR
item::= DEFINIR IDENTIFIER:id ASSIGN CURLY_BRACKET_OPEN definir_subitem CURLY_BRACKET_CLOSE DOT {:
	writer.appendln("CEnsAppareil " + id + " = new CAutreAppareil(\"" + id + "\");");
	while(!stack.isEmpty()) {
		String identifier = stack.pop();
		writer.appendln(id + ".add(" + identifier + ");");
	}
	writer.appendln("ma_liste_ens_appareils.add(" + id + ");");
:};
definir_subitem ::=
	  IDENTIFIER:id {: stack.push(id); :}
	| IDENTIFIER:id COMMA definir_subitem {: stack.push(id); :};

// INTERFACE
item ::= interfaces:i interfaces_subitem {:
	String interfaceName = (String) i;
	while(!stack.isEmpty()) {
		String identifier = stack.pop();
		writer.appendln("CInterface " + identifier + " = new CInterface(\"" + identifier + "\", TypeInterface." + interfaceName.toUpperCase() + ");");
		writer.appendln("ma_liste_interfaces.add(" + identifier + ");");
	}
:};
interfaces_subitem ::=
	  IDENTIFIER:id DOT {: stack.push(id); :}
	| IDENTIFIER:id COMMA interfaces_subitem {: stack.push(id); :};


item ::= POURTOUT pourtout_subitem FAIRE pourtout_action SEMI_COLON FAIT SEMI_COLON {:
	String action = table.remove("POURTOUT_ACTION");

	if(table.get("POURTOUT_APPAREIL") != null) {
		writer.appendln("for(CAppareil appareil : this.l_appareils)");
		String appareil = table.remove("POURTOUT_APPAREIL");
		writer.appendln("\tif (appareil.typeAppareil.equals(TypeAppareil." + appareil.toUpperCase() + "))\\n");
	}

	if(table.get("POURTOUT_AUTRE_APPAREIL") != null) {
		writer.appendln("for(CAppareil appareil : this.l_appareils)");
		table.remove("POURTOUT_AUTRE_APPAREIL");
		writer.appendln("\tif (appareil.estTypeAutreAppareil()\\n");
	}
	
	writer.appendln("\t\tappareil.appliquer(TypeActionAppareil." + action.toUpperCase() + ");\\n");
:};
pourtout_subitem ::=
	  IDENTIFIER:id COLON appareils:a {: table.put("POURTOUT_APPAREIL", (String)a); :}
	| IDENTIFIER:id COLON IDENTIFIER:a {: 
		writer.appendln("for(CEnsAppareil ensAppareil : this.l_ensembles)");
		writer.appendln("\tif (ensAppareil.nomEnsAppareil.equals(\\\"" + ((String)a) + "\\\"))\\n");
		writer.appendln("\t\tfor(CAppareil appareil : ensAppareil.l_appareils)");
	:} 
	| IDENTIFIER:id COLON AUTRE_APPAREIL:a {: table.put("POURTOUT_AUTRE_APPAREIL", (String)a); :}
	| IDENTIFIER:id COLON AUTRE_APPAREIL PARANTHESES_OPEN autre_appareils:a PARANTHESES_CLOSE {:
		table.put("POURTOUT_APPAREIL", "autre_appareil_" + (String)a);
	:};
pourtout_action ::=
	  IDENTIFIER:id DOT actions:aa {: table.put("POURTOUT_ACTION", (String)aa); :}
	| MESSAGE PARANTHESES_OPEN message_subitem PARANTHESES_CLOSE {::};


item ::= IDENTIFIER:id DOT actions:app_action SEMI_COLON {:
	writer.appendln("for(CAppareil appareil : this.l_appareils)");
	writer.appendln("\tif (appareil.getNomAppareil().equals(\\\" " + (String)id + " \\\"))\\n");
	writer.appendln("\t\tappareil.appliquer(TypeActionAppareil." + ((String)app_action).toUpperCase() + ");\\n");
:};
// TO BE UPDATED THIS PART
item ::= SI PARANTHESES_OPEN si_condition PARANTHESES_CLOSE ALORS si_subitem SEMI_COLON {::};
si_condition ::=
	 IDENTIFIER:id DOT ETAT EQUALS states:state  {:
		writer.appendln("for(CAppareil appareil : this.l_appareils)");
		writer.appendln("\tif(appareil.getNomAppareil().equals(\\\"" + id + "\\\"))\\n");
		writer.appendln("\t\tif(appareil.etatAppareil.equals(TypeEtatAppareil.ETEINT))\\n");
		writer.appendln("\t\t\tappareil.appliquer(TypeActionAppareil.ALLUMER);\\n");
	:};
si_subitem ::=
	  IDENTIFIER:id DOT actions:app_action {::}
	| MESSAGE PARANTHESES_OPEN message_subitem PARANTHESES_CLOSE {::};


item ::= SINON sinon_subitem SEMI_COLON FSI SEMI_COLON {::};
sinon_subitem ::=
	  IDENTIFIER:id DOT actions:app_action {::}
	| MESSAGE PARANTHESES_OPEN message_subitem PARANTHESES_CLOSE {::};

// item ::= IDENTIFIER:id DOT ETAT {::};
item ::= IDENTIFIER:id DOT states:app_state SEMI_COLON {::};
item ::= FSI SEMI_COLON {::};

item ::= EXECUTOR_SCENARIO IDENTIFIER:id SEMI_COLON {:
	writer.appendln("this.execScenarioNum(" + scenarii.indexOf(id) + ");\\n");
:};

item ::= MESSAGE PARANTHESES_OPEN message_subitem:message PARANTHESES_CLOSE SEMI_COLON {:
	writer.appendln("System.out.println(\\\"" + message + "\\\");\n");
:};

item ::= ASSOCIER IDENTIFIER:id ASSIGN associer_subitem DOT {:
	while(!stack.isEmpty()) {
		String scenario = stack.pop();
		writer.appendln(id + ".addScenarioAssocie(\"" + scenario +"\");");
	}
:};
associer_subitem ::=
	  IDENTIFIER:id {: stack.push(id); :}
	| IDENTIFIER:id COMMA associer_subitem {: stack.push(id); :}
	| CURLY_BRACKET_OPEN associer_subitem CURLY_BRACKET_CLOSE {::};

item ::= PROGRAMMER IDENTIFIER:id ASSIGN programmer_subset DOT {:
	String className = "p" + ++programmerCount;
	writer.appendln("CProgrammation " + className + " = new CProgrammation(\"" + id + "\");");

	int count = 1;
	while(!stack.isEmpty()) {
		String dateClassName = className + "d" + count;
		writer.appendln("CDate " + dateClassName + " = new CDate" + stack.pop() + ";");
		writer.appendln(className + ".addDate(" + dateClassName + ");");
		count++;
	}
	writer.appendln("ma_liste_programmations.add(" + className + ");");
:};
programmer_subset ::=
	  DATE:date {: stack.push(Utils.fixDate(date)); :}
	| DATE:date COMMA programmer_subset {: stack.push(Utils.fixDate(date)); :}
	| CURLY_BRACKET_OPEN programmer_subset CURLY_BRACKET_CLOSE {::};
