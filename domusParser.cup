import java_cup.runtime.*;
import java.util.*;

parser code {:
	public static void main(String args[]) throws Exception {
		new parser(new Yylex(System.in)).parse();
	}
	
	public void syntax_error(Symbol s) {
    	System.out.println("compiler has detected a syntax error at line " + getLine() + " column " + getColumn());
		System.out.println(getText());
	}

	public int getLine() {
		return ((Yylex) getScanner()).getLine();
	}

	public int getColumn() {
		return ((Yylex) getScanner()).getColumn();
	}
	
	public String getText() {
		return ((Yylex) getScanner()).getText();
	}
:}

action code {:
	DomusProgram domus;
	Scenario scenario;
	List<Appareil> appareils = new ArrayList<Appareil>();

	Hashtable< String, String > table = new Hashtable< String, String >();
	Stack<String> stack = new Stack<String>();
	int programmerCount = 0;
	List scenarii = new ArrayList();
	List<Object> code = new ArrayList<Object>();
:}

terminal PROGRAMME_DOMUS_OPEN_TAG, PROGRAMME_DOMUS_CLOSE_TAG,
 DECLARATION_APPAREILS_OPEN_TAG, DECLARATION_APPAREILS_CLOSE_TAG,
 DECLARATION_INTERFACE_OPEN_TAG, DECLARATION_INTERFACE_CLOSE_TAG,
 DECLARATION_SCENARII_OPEN_TAG, DECLARATION_SCENARII_CLOSE_TAG,
 DECLARATION_COMMANDES_OPEN_TAG, DECLARATION_COMMANDES_CLOSE_TAG,
 
 DEFINIR, EXECUTOR_SCENARIO, ASSOCIER, PROGRAMMER, MESSAGE, POURTOUT,
 FAIRE, FAIT, SI, ALORS, SINON, FSI, OUVRIR, FERMER, ETEINDRE, ALLUMER,
 TAMISER, ETAT, ALLUMER_PARTIEL, ALLUMER_ECO, OUVRIR_PARTIEL, ALLUME, ETEINT,
 DEMI, ECO, OUVERT, FERME, TABLETTE, TV, HIFI, CAFETIERE, VIDEO_PROJ,
 LAVE_VAISSELLE, LAVE_LINGE, SECHE_LINGE, ORDINATEUR, PORTAIL,

 DOT, COMMA, COLON, ASSIGN, SEMI_COLON, CURLY_BRACKET_OPEN, CURLY_BRACKET_CLOSE, EQUALS, PARANTHESES_OPEN, PARANTHESES_CLOSE;

terminal String SCENARIO_OPEN_TAG,SCENARIO_CLOSE_TAG, IDENTIFIER, LINE_COMMENT, BLOCK_COMMENT, STRING_LITERAL, DATE;
// appareils
terminal String ECLAIRAGE, VOLET, CHAUFFAGE, ALARME, FENETRE, AUTRE_APPAREIL;
// interface
terminal String INTERRUPTEUR, MOBILE, TELEPHONE, TELECOMMANDE;

non terminal expr, list, item;
non terminal appareils_subitem, interfaces_subitem, autre_appareils_subitem, definir_subitem;
non terminal pourtout_subitem, pourtout_action, si_condition, si_subitem, sinon_subitem, message_subitem, programmer_subset, associer_subitem;
non terminal appareils, autre_appareils, interfaces, actions, states;

expr ::= list {:
	System.out.println("\n\nSucessfully compiled!");
:} | {: System.out.println("Empty domus file!"); :};

list ::= item list | item;

// COMMENTS
item ::= LINE_COMMENT:s {: code.add(new Comment(s)); :};
item ::= BLOCK_COMMENT:s {: code.add(new Comment(s)); :};

// TAGS
item ::= PROGRAMME_DOMUS_OPEN_TAG:s {: domus = new DomusProgram(); :};
item ::= PROGRAMME_DOMUS_CLOSE_TAG:s {: :};

item ::= DECLARATION_APPAREILS_OPEN_TAG:s {:
	// clean code container before defining appareil
	// code.clear();
	System.out.println(code);
:};
item ::= DECLARATION_APPAREILS_CLOSE_TAG:s {: System.out.println(code);:};

item ::= DECLARATION_INTERFACE_OPEN_TAG:s {: :};
item ::= DECLARATION_INTERFACE_CLOSE_TAG:s {: :};

item ::= DECLARATION_SCENARII_OPEN_TAG:s {: domus.createScenarii(); :};
item ::= DECLARATION_SCENARII_CLOSE_TAG:s {: :};

item ::= SCENARIO_OPEN_TAG:s {:
	scenario = new Scenario(s);

	scenarii.add(scenario.getName());
	table.put("SCENARIO", scenario.getName());
	domus.writerMaisonln("String " + scenario.getName() + "_contenu = \"\\n");

	domus.writerHabitatln(
		"\tcase " + scenarii.indexOf(scenario.getName()) + ": "
		+ "// Scenario " + scenario.getName()
	);
:};
item ::= SCENARIO_CLOSE_TAG:s {:
	String scenario = table.get("SCENARIO");
	domus.writerMaisonln("\";");
	domus.writerMaisonln("CScenario " + scenario + " = new CScenario(\"" + scenario + "\", " + scenario + "_contenu);");
	domus.writerMaisonln("ma_liste_scenarios.add(" + scenario + ");");

	table.remove("SCENARIO");

	domus.writerHabitatln("\tbreak;");
	scenario = null;
:};

item ::= DECLARATION_COMMANDES_OPEN_TAG:s {: :};
item ::= DECLARATION_COMMANDES_CLOSE_TAG:s {: :};

appareils ::= 
	  ECLAIRAGE:a {: RESULT = a; :} 
	| VOLET:a {: RESULT = a; :}
	| CHAUFFAGE:a {: RESULT = a; :}
	| ALARME:a {: RESULT = a; :}
	| FENETRE:a {: RESULT = a; :};

autre_appareils ::= 
	  TABLETTE:ap {: RESULT = "tablette"; :}
	| TV:ap {: RESULT = "tv"; :}
	| HIFI:ap {: RESULT = "hifi"; :}
	| CAFETIERE:ap {: RESULT = "cafe"; :}
	| VIDEO_PROJ:ap {: RESULT = "video_proj"; :}
	| LAVE_VAISSELLE:ap {: RESULT = "lave_vaisselle"; :}
	| LAVE_LINGE:ap {: RESULT = "lave_linge"; :}
	| SECHE_LINGE:ap {: RESULT = "seche_linge"; :}
	| ORDINATEUR:ap {: RESULT = "ordinateur"; :}
	| PORTAIL:ap {: RESULT = "portail"; :};

interfaces ::= 
	  INTERRUPTEUR:i {: RESULT = i; :}
	| MOBILE:i {: RESULT = i; :}
	| TELEPHONE:i {: RESULT = i; :}
	| TELECOMMANDE:i {: RESULT = i; :};

actions ::=
      OUVRIR:a {: RESULT = a; :}
	| FERMER:a {: RESULT = a; :} 
	| ETEINDRE:a {: RESULT = a; :} 
	| ALLUMER :a {: RESULT = a; :}
	| TAMISER:a {: RESULT = a; :}
	| ALLUMER_PARTIEL:a {: RESULT = a; :}
	| ALLUMER_ECO:a {: RESULT = a; :}
	| OUVRIR_PARTIEL:a {: RESULT = a; :};

states ::=
	  ALLUME :s {: RESULT = s; :}
	| ETEINT:s {: RESULT = s; :}
	| DEMI:s {: RESULT = s; :}
	| ECO:s {: RESULT = s; :}
	| OUVERT:s {: RESULT = s; :}
	| FERME:s {: RESULT = s; :};

message_subitem ::=
	  STRING_LITERAL:s {: RESULT = ((String) s).replaceAll("\"", ""); :}
	| STRING_LITERAL:s COMMA IDENTIFIER:id message_subitem {: RESULT = s; :}
	| STRING_LITERAL:s COMMA IDENTIFIER:id DOT ETAT message_subitem {: RESULT = s; :}
	|;


// APPAREIL
item ::= appareils:a appareils_subitem:id {:
	while(!stack.isEmpty()) {
		Appareil appareil = new Appareil(a, stack.pop());
		code.add(appareil);
		domus.writerMaisonln(appareil.toString());
	}
:};
appareils_subitem ::=
	  IDENTIFIER:id DOT {: stack.push(id); :}
	| IDENTIFIER:id COMMA appareils_subitem {: stack.push(id); :};


// AUTRE APPAREIL
item ::= AUTRE_APPAREIL PARANTHESES_OPEN autre_appareils:ap PARANTHESES_CLOSE autre_appareils_subitem {:
	while(!stack.isEmpty()) {
		AutreAppareil autreAppareil = new AutreAppareil(ap, stack.pop());
		code.add(autreAppareil);
		domus.writerMaisonln(autreAppareil.toString());
	}
:};
autre_appareils_subitem ::=
	  IDENTIFIER:id DOT {: stack.push(id); :}
	| IDENTIFIER:id COMMA autre_appareils_subitem {: stack.push(id); :};


// DEFINIR
item::= DEFINIR IDENTIFIER:id ASSIGN CURLY_BRACKET_OPEN definir_subitem CURLY_BRACKET_CLOSE DOT {:
	domus.writerMaisonln("CEnsAppareil " + id + " = new CAutreAppareil(\"" + id + "\");");
	while(!stack.isEmpty()) {
		String identifier = stack.pop();
		domus.writerMaisonln(id + ".add(" + identifier + ");");
	}
	domus.writerMaisonln("ma_liste_ens_appareils.add(" + id + ");");
:};
definir_subitem ::=
	  IDENTIFIER:id {: stack.push(id); :}
	| IDENTIFIER:id COMMA definir_subitem {: stack.push(id); :};

// INTERFACE
item ::= interfaces:i interfaces_subitem {:
	while(!stack.isEmpty()) {
		Interface interf = new Interface(i, stack.pop());
		code.add(interf);
		domus.writerMaisonln(interf.toString());
	}
:};
interfaces_subitem ::=
	  IDENTIFIER:id DOT {: stack.push(id); :}
	| IDENTIFIER:id COMMA interfaces_subitem {: stack.push(id); :};


item ::= POURTOUT pourtout_subitem FAIRE pourtout_action SEMI_COLON FAIT SEMI_COLON {:
	String forStmt = "for(CAppareil appareil : this.l_appareils)";
	String action = table.remove("POURTOUT_ACTION");

	if(table.get("POURTOUT_APPAREIL") != null) {
		domus.writerMaisonln(forStmt);
		domus.writerHabitatln(forStmt);
		String appareil = table.remove("POURTOUT_APPAREIL");
		domus.writerMaisonln("\tif (appareil.typeAppareil.equals(TypeAppareil." + appareil.toUpperCase() + "))\\n");
		domus.writerHabitatln("\tif (appareil.typeAppareil.equals(TypeAppareil." + appareil.toUpperCase() + "))");
	}

	if(table.get("POURTOUT_AUTRE_APPAREIL") != null) {
		domus.writerMaisonln(forStmt);
		domus.writerHabitatln(forStmt);
		table.remove("POURTOUT_AUTRE_APPAREIL");
		domus.writerMaisonln("\tif (appareil.estTypeAutreAppareil()\\n");
		domus.writerHabitatln("\tif (appareil.estTypeAutreAppareil()");
	}
	
	domus.writerMaisonln("\t\tappareil.appliquer(TypeActionAppareil." + action.toUpperCase() + ");\\n");
	domus.writerHabitatln("\t\tappareil.appliquer(TypeActionAppareil." + action.toUpperCase() + ");");
:};
pourtout_subitem ::=
	  IDENTIFIER:id COLON appareils:a {: table.put("POURTOUT_APPAREIL", (String)a); :}
	| IDENTIFIER:id COLON IDENTIFIER:a {: 
		domus.writerMaisonln("for(CEnsAppareil ensAppareil : this.l_ensembles)");
		domus.writerMaisonln("\tif (ensAppareil.nomEnsAppareil.equals(\\\"" + ((String)a) + "\\\"))\\n");
		domus.writerMaisonln("\t\tfor(CAppareil appareil : ensAppareil.l_appareils)");

		// Habitat
		domus.writerHabitatln("for(CEnsAppareil ensAppareil : this.l_ensembles)");
		domus.writerHabitatln("\tif (ensAppareil.nomEnsAppareil.equals(\"" + ((String)a) + "\"))");
		domus.writerHabitatln("\t\tfor(CAppareil appareil : ensAppareil.l_appareils)");
	:}
	| IDENTIFIER:id COLON AUTRE_APPAREIL:a {: table.put("POURTOUT_AUTRE_APPAREIL", (String)a); :}
	| IDENTIFIER:id COLON AUTRE_APPAREIL PARANTHESES_OPEN autre_appareils:a PARANTHESES_CLOSE {:
		table.put("POURTOUT_APPAREIL", "autre_appareil_" + (String)a);
	:};
pourtout_action ::=
	  IDENTIFIER:id DOT actions:aa {: table.put("POURTOUT_ACTION", (String)aa); :}
	| MESSAGE PARANTHESES_OPEN message_subitem PARANTHESES_CLOSE {::};


item ::= IDENTIFIER:id DOT actions:app_action SEMI_COLON {:
	domus.writerMaisonln("for(CAppareil appareil : this.l_appareils)");
	domus.writerMaisonln("\tif (appareil.getNomAppareil().equals(\\\" " + (String)id + " \\\"))\\n");
	domus.writerMaisonln("\t\tappareil.appliquer(TypeActionAppareil." + ((String)app_action).toUpperCase() + ");\\n");

	// Habitat
	domus.writerHabitatln("for(CAppareil appareil : this.l_appareils)");
	domus.writerHabitatln("\tif (appareil.getNomAppareil().equals(\"" + (String)id + "\"))");
	domus.writerHabitatln("\t\tappareil.appliquer(TypeActionAppareil." + ((String)app_action).toUpperCase() + ");");
:};
// TO BE UPDATED THIS PART
item ::= SI PARANTHESES_OPEN si_condition PARANTHESES_CLOSE ALORS si_subitem SEMI_COLON {::};
si_condition ::=
	 IDENTIFIER:id DOT ETAT EQUALS states:state  {:
		domus.writerMaisonln("for(CAppareil appareil : this.l_appareils)");
		domus.writerMaisonln("\tif(appareil.getNomAppareil().equals(\\\"" + id + "\\\"))\\n");
		domus.writerMaisonln("\t\tif(appareil.etatAppareil.equals(TypeEtatAppareil.ETEINT))\\n");
		domus.writerMaisonln("\t\t\tappareil.appliquer(TypeActionAppareil.ALLUMER);\\n");

		// Habitat
		domus.writerHabitatln("for(CAppareil appareil : this.l_appareils)");
		domus.writerHabitatln("\tif(appareil.getNomAppareil().equals(\"" + id + "\"))");
		domus.writerHabitatln("\t\tif(appareil.etatAppareil.equals(TypeEtatAppareil.ETEINT))");
		domus.writerHabitatln("\t\t\tappareil.appliquer(TypeActionAppareil.ALLUMER);");
	:};
si_subitem ::=
	  IDENTIFIER:id DOT actions:app_action {::}
	| MESSAGE PARANTHESES_OPEN message_subitem PARANTHESES_CLOSE {::};


item ::= SINON sinon_subitem SEMI_COLON FSI SEMI_COLON {::};
sinon_subitem ::=
	  IDENTIFIER:id DOT actions:app_action {::}
	| MESSAGE PARANTHESES_OPEN message_subitem PARANTHESES_CLOSE {::};

// item ::= IDENTIFIER:id DOT ETAT {::};
item ::= IDENTIFIER:id DOT states:app_state SEMI_COLON {::};
item ::= FSI SEMI_COLON {::};

item ::= EXECUTOR_SCENARIO IDENTIFIER:id SEMI_COLON {:
	domus.writerMaisonln("this.execScenarioNum(" + scenarii.indexOf(id) + ");\\n");
:};

item ::= MESSAGE PARANTHESES_OPEN message_subitem:message PARANTHESES_CLOSE SEMI_COLON {:
	domus.writerMaisonln("System.out.println(\\\"" + message + "\\\");\n");
:};

item ::= ASSOCIER IDENTIFIER:id ASSIGN associer_subitem DOT {:
	while(!stack.isEmpty()) {
		String scenario = stack.pop();
		domus.writerMaisonln(id + ".addScenarioAssocie(\"" + scenario +"\");");
	}
:};
associer_subitem ::=
	  IDENTIFIER:id {: stack.push(id); :}
	| IDENTIFIER:id COMMA associer_subitem {: stack.push(id); :}
	| CURLY_BRACKET_OPEN associer_subitem CURLY_BRACKET_CLOSE {::};

item ::= PROGRAMMER IDENTIFIER:id ASSIGN programmer_subset DOT {:
	String className = "p" + ++programmerCount;
	domus.writerMaisonln("CProgrammation " + className + " = new CProgrammation(\"" + id + "\");");

	int count = 1;
	while(!stack.isEmpty()) {
		String dateClassName = className + "d" + count;
		domus.writerMaisonln("CDate " + dateClassName + " = new CDate" + stack.pop() + ";");
		domus.writerMaisonln(className + ".addDate(" + dateClassName + ");");
		count++;
	}
	domus.writerMaisonln("ma_liste_programmations.add(" + className + ");");
:};
programmer_subset ::=
	  DATE:date {: stack.push(Utils.fixDate(date)); :}
	| DATE:date COMMA programmer_subset {: stack.push(Utils.fixDate(date)); :}
	| CURLY_BRACKET_OPEN programmer_subset CURLY_BRACKET_CLOSE {::};
